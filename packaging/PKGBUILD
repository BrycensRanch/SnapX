# Maintainer: Brycen Granville <brycengranville@outlook.com>
pkgbase=snapx
pkgname=(snapx snapx-gtk snapx-ui)
pkgver=0.1.0
pkgrel=1
pkgdesc="Screenshot tool that handles images, text, and video (fork of ShareX)"
arch=('x86_64' 'aarch64')
url="https://github.com/BrycensRanch/SnapX"
license=('GPL-3.0-or-later')
makedepends=(
    'git'
    'dotnet-sdk>=9.0'
    'clang'
    'zlib'
)

source=("$pkgbase::git+https://github.com/BrycensRanch/SnapX.git")
sha256sums=('SKIP')

build() {
    cd "$pkgbase"
    export VERSION=$pkgver
    ./build.sh --configuration Release
}

check() {
    cd "$pkgbase"
    Output/snapx/snapx --version
}

package_snapx() {
    depends=(
        'ffmpeg'
        'curl'
        'fontconfig'
        'freetype2'
        'openssl'
        'icu'
        'at'
        'sudo'
        'libxrandr'
        'libxcb'
        'dbus'
    )
    
    cd "$pkgbase"
    ./build.sh install --prefix /usr --dest-dir "${pkgdir}" --skip compile
}

# This is a temporary solution until packaging allows you to install files specific to snapx-ui.
# For now, we delete files we don't want. 
package_snapx-gtk() {
    pkgdesc="SnapX GTK4 UI"
    depends=('snapx' 'gtk4')
    
    cd "$pkgbase"
    ./build.sh install --prefix /usr --dest-dir "${pkgdir}" --skip compile
    
    # Leave only files for GTK UI
    rm -rf "${pkgdir}/usr/lib"
    rm -rf "${pkgdir}/usr/share"
    rm -f "${pkgdir}/usr/bin/snapx"
    rm -f "${pkgdir}/usr/bin/snapx-ui"
}

package_snapx-ui() {
    pkgdesc="SnapX Avalonia-based UI (works best on X11)"
    depends=('snapx')
    
    cd "$pkgbase"
    ./build.sh install --prefix /usr --dest-dir "${pkgdir}" --skip compile
    
    # Keep the files only for Avalonia UI
    rm -rf "${pkgdir}/usr/lib"
    rm -rf "${pkgdir}/usr/share"
    rm -f "${pkgdir}/usr/bin/snapx"
    rm -f "${pkgdir}/usr/bin/snapx-gtk"

} 
